apply plugin: 'com.android.library'

android {
    signingConfigs {
        config {
        }
    }
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 41
        versionName "2.11.0"
    }

    //对于import module方式，compileOptions可以直接删掉
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    /*  以下标识
    //union pay
    libs/UPPayAssistEx.jar
    libs/UPPayPluginEx.jar
    */
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //ali
    implementation(name:'alipaysdk-noutdid-15.8.00.201112210139', ext:'aar')

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.zxing:core:3.3.3'

    // 注意：因为微信SDK从4.0.2版本开始已改用gradle形式，后续Android开发工具包将不再更新，请开发者尽快改用gradle方式编译、更新微信SDK
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:+'

    //网络请求库
    implementation 'com.squareup.okhttp3:okhttp:4.4.0'

    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.mockito:mockito-core:3.6.28"
    testImplementation ('org.powermock:powermock-api-mockito2:2.0.9') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    testImplementation ('org.powermock:powermock-module-junit4:2.0.9') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

//以下用于生成sdk jar
//gradle makeJar
task clearJar(type: Delete) {
    delete fileTree(dir: 'build/libs' , include: 'beecloud*.jar')
}

task makeJar(type:org.gradle.api.tasks.bundling.Jar) {
    //指定生成的jar名
    baseName 'beecloud-2.11.0'
    //从哪里打包class文件
    from('build/intermediates/javac/release/classes/cn/beecloud')
    //打包到jar后的目录结构
    into('cn/beecloud')
    //去掉不需要打包的目录和文件
    exclude('test/', 'BuildConfig.class', 'R.class')
    //去掉R$开头的文件
    exclude{ it.name.startsWith('R$');}
}

makeJar.dependsOn(clearJar, build)

//以下用于生成javadoc
//gradle generateReleaseJavaDoc --info
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        doFirst {
            classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())
        }
        //title = "$name $version API"
        title = "BeeCloud Android SDK API"
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        options {
            encoding 'UTF-8'
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
